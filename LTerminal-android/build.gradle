buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
        classpath 'io.fabric.tools:gradle:1.23.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://s3.amazonaws.com/fabric-artifacts-private/internal-snapshots' }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1022000
        versionName "1.22.0"
        externalNativeBuild {
            cmake {
                targets "LTerminal"
                cppFlags "-std=c++14"
                cFlags "-fexceptions", "-frtti"
                cppFlags "-fexceptions", "-frtti"
                abiFilters 'x86', 'armeabi-v7a'
            }
        }
    }

    signingConfigs {
        release {
            storeFile file("LTerminal_Distribution.keystore")
            storePassword System.getenv("KEY_PASSWORD")
            keyAlias "LTerminal_Distribution"
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs         = ['src']
            resources.srcDirs    = []
            aidl.srcDirs         = []
            renderscript.srcDirs = []
            res.srcDirs          = ['res']
            assets.srcDirs       = ['assets']
            jniLibs.srcDirs      = ['libs']
        }
    }

    dexOptions {
        preDexLibraries = false
    }

    compileOptions {
        incremental = false
    }
}

dependencies {
    compile project(':LFLApp')
    compile project(':LFLCore')
    compile project(':LFLAds')
    compile project(':LFLBilling')
    compile project(':appirater')
    compile('com.crashlytics.sdk.android:crashlytics:2.7.0-SNAPSHOT@aar') {
        transitive = true;
    }
    compile('com.crashlytics.sdk.android:crashlytics-ndk:1.2.0-SNAPSHOT:debug@aar') {
        transitive = true;
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

crashlytics {
    enableNdk true
}
